configfile: "config.yaml"

outdir          = config["data"]["outdir"]
blmin_opts      = config["parameters"]["blmin"]
blmax_opts      = config["parameters"]["blmax"]

raxml_command   = config["parameters"]["raxml-ng"]["command"]
iqtree_command  = config["parameters"]["iqtree"]["command"]

file_name       = "blmin_{blmin}_blmax_{blmax}"
base_dir        = "result_blmin_{blmin}_blmax_{blmax}/"
full_dir        = "{outdir}/" + base_dir

full_dir_raxml          = full_dir + "raxml-ng/"
full_file_path_raxml    = full_dir_raxml + file_name

full_dir_iqtree         = full_dir + "iqtree/"
full_file_path_iqtree   = full_dir_iqtree + file_name

#full_file_path  = full_dir + file_name


rule all:
    input:
        # parameter settings for each run
        expand(f"{full_dir}parameters.json", blmin=blmin_opts, blmax=blmax_opts, outdir=outdir),

        # best Tree and its model
        expand(f"{full_file_path_raxml}.raxml.bestTree", blmin=blmin_opts, blmax=blmax_opts, outdir=outdir),
        expand(f"{full_file_path_raxml}.raxml.bestModel", blmin=blmin_opts, blmax=blmax_opts, outdir=outdir),

        # all trees
        expand(f"{full_file_path_raxml}.raxml.mlTrees", blmin=blmin_opts, blmax=blmax_opts, outdir=outdir),

        # pairwise rf distances for all trees found for one combination of parameters
        expand(f"{full_file_path_raxml}.raxml.rfDistances", blmin=blmin_opts, blmax=blmax_opts, outdir=outdir),

        # pairwise rf distances for all trees
        expand("{outdir}/bestTrees.raxml.rfDistances", outdir=outdir),

        # iqtree significance test results
        expand(f"{full_file_path_iqtree}.iqtree", blmin=blmin_opts, blmax=blmax_opts, outdir=outdir),

        expand(f"{outdir}/results.sqlite3", outdir=outdir),
        

rule save_parameter_settings_in_file:
    output:
        params = f"{full_dir}parameters.json"
    run:
        import json
        data = {}
        data["blmin"] = wildcards.blmin
        data["blmax"] = wildcards.blmax
        with open(output.params, 'w') as f:
            json.dump(data, f)


include: "rules/tree_search.smk"
include: "rules/iqtree_tests.smk"
include: "rules/rf_distances.smk"
include: "rules/save_results_to_database.smk"
